plugins {
    id("com.github.johnrengelman.shadow") version "7.0.0"
    id("io.micronaut.application") version "2.0.3"
    id("org.javamodularity.moduleplugin") version "1.8.10"
    id("org.openrewrite.rewrite") version("5.13.1")
}

version = rootProject.file('VERSION').text.trim()
group = "devex"

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

micronaut {
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("devex.*")
    }
}

dependencies {
    // patch vulnerable dependencies
    implementation("com.fasterxml.jackson.core:jackson-databind:2.9.10.7")
    implementation("io.netty:netty-codec:4.1.68.Final")

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    annotationProcessor("info.picocli:picocli-codegen")
    implementation("info.picocli:picocli")
    implementation("io.micronaut:micronaut-http-client") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'io.netty', module: 'netty-codec'
    }
    implementation("io.micronaut:micronaut-runtime") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    implementation("io.micronaut.picocli:micronaut-picocli") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }

    implementation("io.micronaut:micronaut-validation")

    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-classic:1.2.3'


    implementation 'io.vavr:vavr:0.10.3'
    implementation("io.vavr:vavr-jackson:0.10.3") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }

    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.11.0.202103091610-r'
    implementation('org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:5.11.0.202103091610-r') {
        exclude module: 'jsch'
    }

// original dependency was a multi-version jar and the classes for Java15 were no loading
//    implementation 'com.github.mwiede:jsch:0.1.62'
    implementation 'com.github.miguelaferreira:jsch:replace-tag-jsch-0.1.63-SNAPSHOT'

    implementation 'org.apache.commons:commons-exec:1.3'

    // Test dependencies
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.assertj:assertj-vavr:0.4.1'

    testImplementation("org.junit.jupiter:junit-jupiter-engine") // make gradle compileTestJava work

}

rewrite {
    // enable recipes for specific migrations
}

application {
    mainClass.set("devex.DevexCommand")
}

java {
    modularity.inferModulePath.set(true)
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

test {
    environment "GITLAB_TOKEN", System.getenv('GITLAB_TOKEN')
    environment "GITHUB_TOKEN", System.getenv('GITHUB_TOKEN')

    moduleOptions {
        runOnClasspath = true
    }

//    // To trouble shoot failed tests in CI workflows:
//    // - uncomment the block bellow
//    // - set '--info' in the gradle build step of the workflow
//    testLogging {
//        // set options for log level LIFECYCLE
//        events "failed"
//        exceptionFormat "short"
//
//        info.showStandardStreams = true
//    }
}

// Workaround for using gradle 7.3 with java 17
// src: https://github.com/micronaut-projects/micronaut-gradle-plugin/issues/296
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

processResources {
    from("VERSION")
}

modularity.patchModule('java.annotation', 'jsr305-3.0.2.jar')
modularity.patchModule('org.eclipse.jgit', 'org.eclipse.jgit.ssh.jsch-5.11.0.202103091610-r.jar')
modularity.patchModule('io.micronaut.runtime', "micronaut-context-${ext.micronautVersion}.jar")

mainClassName = "$moduleName/devex.DevexCommand"
